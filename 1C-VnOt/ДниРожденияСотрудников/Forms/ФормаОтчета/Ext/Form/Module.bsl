#Область СлужебныеПроцедурыИФункции 

&НаСервере
Процедура ВосстановитьНастройки()
	
	Для Каждого ТекЭлемент Из Отчет.КомпоновщикНастроек.Настройки.Отбор.Элементы Цикл
		Если ТекЭлемент.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Организация") Тогда
			ТекЭлемент.ПравоеЗначение = Константы.ОсновнаяОрганизация.Получить();
			ТекЭлемент.Использование = Истина;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры 

&НаСервере
Процедура ЗаполнитьСотрудников(пСписокСотрудников)
	Если ТипЗнч(пСписокСотрудников) = Тип("Строка") Тогда
		лТаблица = ПолучитьИзВременногоХранилища(пСписокСотрудников);
		лТаблица.Свернуть("Сотрудник");
		Список = Новый СписокЗначений;
		Список.ЗагрузитьЗначения(лТаблица.ВыгрузитьКолонку("Сотрудник"));
		Отчет.КомпоновщикНастроек.ПользовательскиеНастройки.Элементы[3].ПравоеЗначение = Список;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура СформироватьОтчёт()
	
	ПериодТ = ТекущаяДатаСеанса();
	ДГТД = ДеньГода(ПериодТ);
	ДГ = Новый СписокЗначений;
	ТД = ДатаНачала;
	ДГ.Добавить(ДеньГода(ТД-86400));
	Пока ТД <= ДатаОкончания Цикл
		ДГ.Добавить(ДеньГода(ТД));
		Если ДеньГода(ТД) = ДГТД Тогда
			ПериодТ = Дата(Год(ТД), Месяц(ТД), День(ТД));	
		КонецЕсли;
		ТД = ТД + 86400;
	КонецЦикла;

	ПараметрПериод = Новый ПараметрКомпоновкиДанных("Период");
	ПараметрДН = Новый ПараметрКомпоновкиДанных("ДатаНачала");
	ПараметрДК = Новый ПараметрКомпоновкиДанных("ДатаОкончания");
	ПараметрДГ = Новый ПараметрКомпоновкиДанных("ДниГода");
	Для Каждого ТекПараметр Из Отчет.КомпоновщикНастроек.Настройки.ПараметрыДанных.Элементы	Цикл
		Если ТекПараметр.Параметр = ПараметрПериод Тогда
			ТекПараметр.Значение = ПериодТ;
			ТекПараметр.Использование = Истина;
		КонецЕсли; 	
		Если ТекПараметр.Параметр = ПараметрДН Тогда
			ТекПараметр.Значение = ДатаНачала;
			ТекПараметр.Использование = Истина;
		КонецЕсли; 		
		Если ТекПараметр.Параметр = ПараметрДК Тогда
			ТекПараметр.Значение = КонецДня(ДатаОкончания);
			ТекПараметр.Использование = Истина;
		КонецЕсли; 	
		Если ТекПараметр.Параметр = ПараметрДГ Тогда
			ТекПараметр.Значение = ДГ;
			ТекПараметр.Использование = Истина;
		КонецЕсли; 	
	КонецЦикла;
	
	СкомпоноватьРезультат();	
	 	    	
КонецПроцедуры 

// Обработка оповещения
&НаКлиенте
Процедура ПослеПодтверждения(Результат, Параметры) Экспорт
	
	Если Результат <> Неопределено И Результат <> "" Тогда
		ЗаполнитьСотрудников(Результат);
	КонецЕсли;
	            
КонецПроцедуры

// Обработка оповещения
&НаКлиенте
Процедура ПослеВыбора(Результат, Параметры) Экспорт
	
	Если Результат <> Неопределено И Результат.Значение = 2 Тогда
		лПараметры = Новый Структура("Организация", Организация);
		Оповещение = Новый ОписаниеОповещения("ПослеПодтверждения", ЭтаФорма);
		ОткрытьФорму("Обработка.ПодборСотрудников.Форма.ФормаОсновная", лПараметры,,,,,Оповещение);
	ИначеЕсли Результат <> Неопределено И Результат.Значение = 1 Тогда
		СотрудникиЗаполнены = Истина;
	КонецЕсли;
	            
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийФормы  

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ВосстановитьНастройки();
	ДатаНачала = НачалоМесяца(ТекущаяДатаСеанса());
	ДатаОкончания = КонецМесяца(ДатаНачала);
	Организация = ХранилищаНастроек.НастройкиПользователей.Загрузить("ОсновнаяОрганизация");
	Если НЕ ЗначениеЗаполнено(Организация) Тогда
		Организация = Константы.ОсновнаяОрганизация.Получить();
	КонецЕсли;
	ЭтаФорма.АвтоОтображениеСостояния = РежимАвтоОтображенияСостояния.НеОтображать;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы 

&НаКлиенте
Процедура КомандаСформировать(Команда)
	
	СформироватьОтчёт();
    	
КонецПроцедуры

// Процедура - обработчик оповещения
&НаКлиенте
Процедура ПослеВыбораПериода(Результат, Параметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		ДатаНачала = Результат.ДатаНачала;
		ДатаОкончания = Результат.ДатаОкончания;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ВыбратьПериод(Команда)
	
	Диалог = Новый ДиалогРедактированияСтандартногоПериода();
	ПериодОтчета = Новый СтандартныйПериод;
	ПериодОтчета.ДатаНачала = ДатаНачала;
	ПериодОтчета.ДатаОкончания = ДатаОкончания;
	Диалог.Период = ПериодОтчета;
	
	Оповещение = Новый ОписаниеОповещения("ПослеВыбораПериода", ЭтотОбъект);
	Диалог.Показать(Оповещение);
		
КонецПроцедуры

&НаКлиенте
Процедура КомпоновщикНастроекПользовательскиеНастройкиПриАктивизацииЯчейки(Элемент)
	Если Элемент.ТекущиеДанные <> Неопределено И Элемент.ТекущиеДанные.Настройка = "Сотрудник" И СокрЛП(Элемент.ТекущиеДанные.ВидСравнения) = "В списке" И (Элемент.ТекущийЭлемент.Имя = "КомпоновщикНастроекПользовательскиеНастройкиЗначение" ИЛИ Элемент.ТекущийЭлемент.Имя = "КомпоновщикНастроекПользовательскиеНастройкиКартинкаЗначения") И НЕ СотрудникиЗаполнены Тогда
		Сп = Новый СписокЗначений;
		Сп.Добавить(1,"Выбрать вручную");
		Сп.Добавить(2,"Использовать помощник подбора");
		Оповещение = Новый ОписаниеОповещения("ПослеВыбора", ЭтаФорма);
		ПоказатьВыборИзСписка(Оповещение, Сп, Элемент.ТекущийЭлемент);
		СотрудникиЗаполнены = Истина;
	Иначе
		СотрудникиЗаполнены = Ложь;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти
